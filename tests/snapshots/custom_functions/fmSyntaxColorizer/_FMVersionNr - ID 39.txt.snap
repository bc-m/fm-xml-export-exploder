---
source: src/main.rs
expression: output_content
---
/* _FMVersionNr( pPrecision.123 )

Gibt die Versionsnummer von FileMaker (bzw. der akt. Programm) als Text bzw. als Zahl zum Vergleichen zurück.

Param & Rückgabewerte (am (fiktiven) Beispiel FileMaker Version "14.5.2"):

  G_Sy_FMVersionNr( "" ) ->  "14.5.2" // Achtung: in fm<14 gibt eine v zurück: "12.5v2"
  G_Sy_FMVersionNr( "." ) -> "14.5"
  G_Sy_FMVersionNr( 1 ) -> 14
  G_Sy_FMVersionNr( 2 ) -> 14,5
  G_Sy_FMVersionNr( 3 ) -> 14,500002

Hinweis: Falls jeine Minorversionsnummer >= 10 je vorkommt, wird eine extra 9 eingefügt, so dass die Zahlen noch verglichen werden können:

  G_Sy_FMVersionNr( "" ) -> "14.11v1"
  G_Sy_FMVersionNr( 2 ) -> 14,911

2021-03-22 RW: Version 2.1.1 Bug#4890: Korrigieren für FM19.2: MinorNr als NUMMER formatieren (um verwirrende 19.92 zu vermeiden)
2015-04-09 RW: Version 2.1 Dev#2567: Erweitert für fm14
2013-01-08 RW: Version 2.0.1 Paramname standardisiert
2012-08-29 RW: Version 2.0 mit Parameter Präzision
2012-06-19 RW: Version 1.0
*/




SetzeVar(
[
vVersionString = WörterRechts( Hole ( ProgrammVersion ) ; 1 ) ;
vPosPunkt2 = Position( vVersionString ; "." ; 1 ; 2 ) ;
vVersionStringMitV = Falls( vPosPunkt2 > 0 ; Ersetzen( vVersionString ; vPosPunkt2 ; 1 ; "v" ) ; vVersionString ) ;
vPos = Position( vVersionStringMitV  ; "v" ; 1 ;1 ) ;
vMajorMinorString = ZeichenLinks( vVersionStringMitV ; vPos - 1) ;
vMinorNr = LiesAlsZahl( ZeichenMitte( vMajorMinorString ; Position( vMajorMinorString ; "." ; 1 ; 1 ) + 1 ; 999 ) ) ;
vMajorMinorNr = LiesAlsZahl( Austauschen( vMajorMinorString ; ["." ; ZeichenLinks( 1/2 ; 1 ) & Falls( vMinorNr >=10 ; "9" )] ) ) 
];
Falls( IstLeer( pPrecision.123 ) ; vVersionString ; 
Auswahl( pPrecision.123 ;
/* "." */ vMajorMinorString ;
/* 1 */ NächsteKleinereGanzzahl( vMajorMinorNr ) ;
/* 2 */ vMajorMinorNr ;
/* 3 */ vMajorMinorNr + (ZeichenRechts( vVersionString ; Länge( vVersionString ) - vPos ) / 1000000)
)))
